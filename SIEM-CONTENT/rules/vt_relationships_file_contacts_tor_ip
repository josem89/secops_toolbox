rule vt_relationships_file_contacts_tor_ip {
  meta:
    author = "Chronicle Security"
    description = "Alert on known Hash contacting known IP with VT Relationships."
    severity = "High"
    priority = "High"

  events:
    // event
    $e.metadata.event_type = "NETWORK_CONNECTION"
    $e.principal.process.file.sha256 = $file_hash
    $e.target.ip = $ip
    $e.principal.hostname = $hostname

    // principal
    $vt.graph.metadata.entity_type = "FILE"
    $vt.graph.metadata.source_type = "GLOBAL_CONTEXT"
    $vt.graph.metadata.vendor_name = "VirusTotal"
    $vt.graph.metadata.product_name = "VirusTotal Relationships"
    $vt.graph.entity.file.sha256 = $file_hash

    // target
    $vt.graph.relations.entity_type = "IP_ADDRESS"
    $vt.graph.relations.relationship = "CONTACTS"
    $vt.graph.relations.entity.ip = $ip

    // Tor IP
    $gcti_feed.graph.entity.artifact.ip = $ip
    $gcti_feed.graph.metadata.entity_type = "IP_ADDRESS"
    $gcti_feed.graph.metadata.threat.threat_feed_name = "Tor Exit Nodes"
    $gcti_feed.graph.metadata.vendor_name = "Google Cloud Threat Intelligence"
    $gcti_feed.graph.metadata.product_name = "GCTI Feed"

  match:
    $hostname over 5m

  outcome: 
    $risk_score = max(
        if($gcti_feed.graph.metadata.threat.confidence = "HIGH_CONFIDENCE", 70) +
        // Unauthorized target geographies
        if($e.target.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($e.target.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($e.target.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($e.target.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($e.target.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $malicious_files = array_distinct($e.principal.process.file.full_path)
    $tor_ips = array_distinct($ip)
    $file_hashes = array_distinct($file_hash)
    $count_file_hash = count_distinct($file_hash)
    $count_tor_ips = count_distinct($ip)
    $tor_geoip_country = array_distinct($e.target.ip_geo_artifact.location.country_or_region)
    $tor_geoip_state = array_distinct($e.target.ip_geo_artifact.location.state)
    $target_url = array_distinct($e.target.url)
    $user_email = array_distinct($e.principal.user.email_addresses)

  condition:
    $e and $vt and $gcti_feed
}

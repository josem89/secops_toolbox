rule okta_mfa_brute_force_attack {

  meta:
    author = "Chronicle Security"
    description = "Detects a successful login after multiple failed MFA pushes"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/"
    mitre_attack_version = "v2.5"
    severity = "Medium"
    priority = "Medium"

  events:
    $push.metadata.product_name = "Okta"
    $push.metadata.vendor_name = "Okta"
    $push.metadata.event_type = "USER_UNCATEGORIZED"
    $push.metadata.product_event_type = "system.push.send_factor_verify_push"
    $push.target.user.userid = $userid

    $auth.metadata.event_type = "USER_LOGIN"
    $auth.metadata.product_event_type = "user.authentication.auth_via_mfa"
    $auth.metadata.product_name = "Okta"
    $auth.metadata.vendor_name = "Okta"
    $auth.security_result.action = "ALLOW"
    $auth.target.user.userid = $userid

    $push.metadata.event_timestamp.seconds <= $auth.metadata.event_timestamp.seconds

  match:
    $userid over 15m

  outcome:
    $pushTargetBrowser = array_distinct($push.network.http.parsed_user_agent.browser)
    $pushPrincipalIp = array_distinct($push.principal.ip)
    $pushPrincipalIpCountry = array_distinct($push.principal.ip_geo_artifact.location.country_or_region)
    $pushPrincipalIpState = array_distinct($push.principal.ip_geo_artifact.location.state)
    $pushPrincipalIpCity = array_distinct($push.principal.location.city)
    $pushPrincipalUserEmailAddresses = array_distinct ($push.principal.user.email_addresses)
    $pushSecurityResultSummary = array_distinct($push.security_result.summary)
    $pushTargetUserEmailAddresses = array_distinct($push.target.user.email_addresses)
    $pushTargetUserUserid = array_distinct($push.target.user.userid)
    $authTargetBrowser = array_distinct($auth.network.http.parsed_user_agent.browser)
    $authPrincipalIp = array_distinct($auth.principal.ip)
    $authPrincipalIpCountry = array_distinct($auth.principal.ip_geo_artifact.location.country_or_region)
    $authPrincipalIpState = array_distinct($auth.principal.ip_geo_artifact.location.state)
    $authPrincipalIpCity = array_distinct($auth.principal.location.city)
    $authPrincipalUserEmailAddresses = array_distinct ($auth.principal.user.email_addresses)
    $authSecurityResultSummary = array_distinct($auth.security_result.summary)
    $authTargetUserEmailAddresses = array_distinct($auth.target.user.email_addresses)
    $authTargetUserUserid = array_distinct($auth.target.user.userid)

  condition:
    #push > 4 and $auth
}

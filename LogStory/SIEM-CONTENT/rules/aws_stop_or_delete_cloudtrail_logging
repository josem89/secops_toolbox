rule aws_stop_or_delete_cloudtrail_logging {

  meta:
    author = "Chronicle Security"
    description = "Detects when CloudTrail logging is stopped or deleted."
    mitre_attack_tactic = "Defense Evasion"
    mitre_attack_technique = "Impair Defenses: Disable Cloud Logs"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1562/008/"
    mitre_attack_version = "v1.3"
    severity = "High"
    priority = "High"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    ($cloudtrail.metadata.product_event_type = "StopLogging" or $cloudtrail.metadata.product_event_type = "DeleteTrail")
    $cloudtrail.security_result.action = "ALLOW"
    $cloudtrail.principal.user.user_display_name = $userId

  match:
    $userId over 1h

  outcome:
    $risk_score = max(
        // Unauthorized geographies
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Cuba", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Iran", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Russia", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Syria", 40)
    )
    $networkHttpUserAgent = array_distinct($cloudtrail.network.http.user_agent)
    $principalIp = array_distinct($cloudtrail.principal.ip)
    $principalIpCountry = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principalUserDisplayName = array_distinct($cloudtrail.principal.user.user_display_name)
    $awsAccount = array_distinct($cloudtrail.target.resource.attribute.labels["Recipient Account Id"])
    $awsRegion = array_distinct($cloudtrail.principal.location.name)

  condition:
    $cloudtrail
}
  
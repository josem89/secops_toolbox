rule okta_mismatch_between_source_and_response_for_verify_push_request {

  meta:
    author = "Chronicle Security"
    description = "Okta Mismatch Between Source and Response for Verify Push Request"
    vendor_documentation = "https://sec.okta.com/fastpassphishingdetection"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Multi-Factor Authentication Request Generation"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1621"
    mitre_attack_version = "v1.0"
    severity = "Medium"
    priority = "Medium"

  events:
    $push.metadata.product_name = "Okta"
    $push.metadata.vendor_name = "Okta"
    $push.metadata.event_type = "USER_UNCATEGORIZED"
    $push.metadata.product_event_type = "system.push.send_factor_verify_push"
    $push.security_result.description = /New Device=POSITIVE/
    $push.security_result.description = /New IP=POSITIVE/
    $push.security_result.action = "ALLOW"
    $push.network.parent_session_id = $parentSessionId

    $auth.metadata.product_name = "Okta"
    $auth.metadata.vendor_name = "Okta"
    $auth.metadata.event_type = "USER_LOGIN"
    $auth.metadata.product_event_type = "user.authentication.auth_via_mfa"
    $auth.security_result.detection_fields["factor"] = "OKTA_VERIFY_PUSH"
    $auth.security_result.action = "ALLOW"
    $auth.network.parent_session_id = $parentSessionId

    $push.metadata.event_timestamp.seconds <= $auth.metadata.event_timestamp.seconds

  match:
    $parentSessionId over 5m

  outcome:
    $risk_score = max(
        // If the IP Address is marked as suspicious IP address by Okta ThreatInsight
        if($push.security_result.detection_fields["threatSuspected"] = "true", 30) +
        // Based on Okta Behavior Detection pattern analysis
        if($push.security_result.description = /New Geo-Location=POSITIVE/, 10) +
        if($push.security_result.description = /New Device=POSITIVE/, 5) +
        if($push.security_result.description = /New IP=POSITIVE/, 5) +
        if($push.security_result.description = /New City=POSITIVE/, 5) +
        if($push.security_result.description = /New State=POSITIVE/, 10) +
        if($push.security_result.description = /New Country=POSITIVE/, 10) +
        if($push.security_result.description = /Velocity=POSITIVE/, 10) +
        // Unauthorized target geographies
        if($push.target.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $pushPrincipalIpCountry = array_distinct($push.principal.ip_geo_artifact.location.country_or_region)
    $pushPrincipalIpState = array_distinct($push.principal.ip_geo_artifact.location.state)
    $pushPrincipalIpCity = array_distinct($push.principal.location.city)
    $pushPrincipalUserEmailAddresses = array_distinct ($push.principal.user.email_addresses)
    $pushSecurityResultSummary = array_distinct($push.security_result.summary)
    $pushTargetUserEmailAddresses = array_distinct($push.target.user.email_addresses)
    $pushTargetUserUserid = array_distinct($push.target.user.userid)
    $authTargetBrowser = array_distinct($push.network.http.parsed_user_agent.browser)
    $authPrincipalIp = array_distinct($push.principal.ip)
    $authPrincipalIpCountry = array_distinct($push.principal.ip_geo_artifact.location.country_or_region)
    $authPrincipalIpState = array_distinct($push.principal.ip_geo_artifact.location.state)
    $authPrincipalIpCity = array_distinct($push.principal.location.city)
    $authPrincipalUserEmailAddresses = array_distinct ($push.principal.user.email_addresses)
    $authSecurityResultSummary = array_distinct($push.security_result.summary)
    $authTargetUserEmailAddresses = array_distinct($push.target.user.email_addresses)
    $authTargetUserUserid = array_distinct($push.target.user.userid)

  condition:
    $push and $auth
}

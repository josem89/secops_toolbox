rule high_risk_user_download_executable_from_macro {
  meta:

    author = "Chronicle Security"
    description = "Executable downloaded by Microsoft Excel from High Risk User"
    severity = "High"
    priority = "High"
    technique = "T1204.002"


  events:

    //proxy event with suspected executable download
      $proxy_event.target.url =  /.*\.exe$/ or
      $proxy_event.network.received_bytes > 102400
      $proxy_event.principal.user.userid = $user
      $proxy_event.principal.user.userid != /test/
      $proxy_event.target.hostname = $hostname


    //correlate with EDR event indicating Excel activity
     $edr_event.target.user.userid  = $user
     $edr_event.target.process.file.full_path = /excel/ nocase
     ($edr_event.metadata.event_type = "NETWORK_HTTP" or 
     $edr_event.metadata.event_type = "NETWORK_CONNECTION")
     $edr_event.target.hostname = $hostname


    //First hop into entity graph to find the permissions
     $user_entity.graph.entity.user.userid = $user
     $user_entity.graph.metadata.entity_type = "USER"
     $user_entity.graph.relations.entity.resource.name != ""
     $user_entity.graph.relations.entity.resource.name = $table_name

    //second hop for the table context
      $table_context.graph.entity.resource.product_object_id = $table_name
      $table_context.graph.metadata.entity_type = "RESOURCE"
      $table_context.graph.metadata.product_name = "GCP DLP CONTEXT"
   match:
     $user,$hostname over 5m

  outcome:
   //calculate risk score
      $risk_score = max(
        if(     $table_context.graph.entity.resource.attribute.labels.value = "US_SOCIAL_SECURITY_NUMBER", 80)
        )
      $technique = array_distinct("T1204.002")
      $principalHostname = array_distinct($proxy_event.principal.hostname)
      $principalIp = array_distinct($proxy_event.principal.ip)
      $principalMac = array_distinct($proxy_event.principal.mac)
      $targetHostname = array_distinct($proxy_event.target.hostname)
      $target_url = array_distinct($proxy_event.target.url)
      $targetIp = array_distinct($proxy_event.target.ip)
      $principalUserUserid =  array_distinct($proxy_event.principal.user.userid)
      $entity_resource_name = array_distinct($table_context.graph.entity.resource.name)

  condition:
    $proxy_event and $edr_event and $user_entity and $table_context  
}


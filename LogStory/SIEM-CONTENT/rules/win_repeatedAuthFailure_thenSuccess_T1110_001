rule win_repeatedAuthFailure_thenSuccess_T1110_001 {

  meta:
    author = "Chronicle Security"
    description = "Detect Multiple Failed Login Attempts followed by Successful Login"
    severity = "Medium"
    priority = "Medium"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force: Password Guessing"
    mitre_url = "https://attack.mitre.org/techniques/T1110/001/"

  events:
   $fail.metadata.event_type      = "USER_LOGIN"
   $fail.metadata.vendor_name     = "Microsoft"
   $fail.principal.hostname       = $principalHost
   $fail.target.user.userid       = $targetUser
   $fail.security_result.action   = "BLOCK"
   
   $fail.metadata.event_timestamp.seconds < $success.metadata.event_timestamp.seconds
   
   $success.metadata.event_type   = "USER_LOGIN"
   $success.target.user.userid    = $targetUser
   $success.principal.hostname    = $principalHost
   $success.security_result.action = "ALLOW"
   $success.metadata.description != "4648"

  match:
   $targetUser, $principalHost over 15m

  outcome:
  $risk_score = max(if($principalHost = /activedir/, 75, 50))
  $impacted_systems = array_distinct($principalHost)
  $impacted_users = array_distinct($targetUser)
  $alert_type = array_distinct("Successful Brute Force Attack")
  $tlp = array_distinct("red")

 
 condition:
   #fail > 4 and $success
}

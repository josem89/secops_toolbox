rule whois_expired_domain_accessed {
  meta:
    author = "Chronicle Security"
    description = "Example usage of WHOIS data, detecting an executable file download from a domain that's recently expired"
    severity = "LOW"
    priority = "Low"

  events:
    $access.metadata.event_type = "NETWORK_HTTP"
    $hostname = $access.target.hostname

    // join access event to entity graph to use WHOIS data
    $whois.graph.entity.domain.name = $access.target.hostname

    // use WHOIS data to look for expired domains
    $whois.graph.metadata.entity_type = "DOMAIN_NAME"
    $whois.graph.metadata.vendor_name = "WHOIS"

    // Domain expired before the event
    $whois.graph.entity.domain.expiration_time.seconds < $access.metadata.event_timestamp.seconds 

  match:
    $hostname over 1h
  
  outcome:
    $principalHostname = array_distinct($access.principal.hostname)
    $principalIp = array_distinct($access.principal.ip)
    $principalMac = array_distinct($access.principal.mac)
    $targetIp = array_distinct($access.target.ip)
    $targetHostname = array_distinct($access.target.hostname)
    $targetUrl = array_distinct($access.target.url)

 condition:
    $access and $whois
}

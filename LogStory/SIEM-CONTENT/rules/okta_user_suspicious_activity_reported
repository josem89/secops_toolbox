rule okta_user_suspicious_activity_reported {

  meta:
    author = "Chronicle Security"
    description = "An Okta user reports suspicious activity in response to an end user security notification."
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v1.4"
    severity = "Medium"
    priority = "Medium"

  events:
    $suspicious.metadata.product_name = "Okta"
    $suspicious.metadata.vendor_name = "Okta"
    $suspicious.metadata.event_type = "USER_UNCATEGORIZED"
    $suspicious.metadata.product_event_type = "user.account.report_suspicious_activity_by_enduser"
    $suspicious.security_result.summary = "User report suspicious activity"
    $suspicious.target.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $targetBrowser = array_distinct($suspicious.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($suspicious.principal.ip)
    $principalIpCountry = array_distinct($suspicious.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($suspicious.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($suspicious.principal.location.city)
    $principalUserEmailAddresses = array_distinct ($suspicious.principal.user.email_addresses)
    $securityResultSummary = array_distinct($suspicious.security_result.summary)
    $targetUserEmailAddresses = array_distinct($suspicious.target.user.email_addresses)
    $targetUserUserid = array_distinct($suspicious.target.user.userid)

  condition:
    $suspicious
}

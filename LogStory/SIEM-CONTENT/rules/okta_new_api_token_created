rule okta_new_api_token_created {

  meta:
    author = "Chronicle Security"
    description = "Detects when a new API token is created."
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v2.6"
    severity = "Medium"
    priority = "Medium"

  events:
    $token.metadata.product_name = "Okta"
    $token.metadata.vendor_name = "Okta"
    $token.metadata.product_event_type = "system.api_token.create"
    $token.security_result.summary = "Create API token"
    $token.security_result.action = "ALLOW"
    $token.principal.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $targetBrowser = array_distinct($token.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($token.principal.ip)
    $principalIpCountry = array_distinct($token.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($token.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($token.principal.location.city)
    $principalUserEmailAddresses = array_distinct ($token.principal.user.email_addresses)
    $securityResultSummary = array_distinct($token.security_result.summary)

  condition:
    $token
}

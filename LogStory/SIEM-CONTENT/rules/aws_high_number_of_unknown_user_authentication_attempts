rule aws_high_number_of_unknown_user_authentication_attempts {

  meta:
    author = "Chronicle Security"
    description = "Detects when a high number of failed authentication attempts happen for unknown users."
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force: Credential Stuffing"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/004/"
    mitre_attack_version = "v1.3"
    severity = "Low"
    priority = "Low"

  events:
    $fail.metadata.vendor_name = "AMAZON"
    $fail.metadata.product_name = "AWS CloudTrail"
    $fail.metadata.event_type = "USER_LOGIN"
    $fail.metadata.product_event_type = "ConsoleLogin"
    $fail.security_result.action = "BLOCK"
    $fail.security_result.description = "Reason: No username found in supplied account"
    $fail.principal.ip = $principal_ip 

  match:
    $principal_ip over 10m

  outcome:
    $risk_score = max(
        // Unauthorized geographies
        if($fail.principal.ip_geo_artifact.location.country_or_region = "Cuba", 40) +
        if($fail.principal.ip_geo_artifact.location.country_or_region = "Iran", 40) +
        if($fail.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 40) +
        if($fail.principal.ip_geo_artifact.location.country_or_region = "Russia", 40) +
        if($fail.principal.ip_geo_artifact.location.country_or_region = "Syria", 40)
    )
    $networkHttpUserAgent = array_distinct($fail.network.http.user_agent)
    $principalIp = array_distinct($fail.principal.ip)
    $principalIpCountry = array_distinct($fail.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($fail.principal.ip_geo_artifact.location.state)
    $principalUserDisplayName = array_distinct($fail.principal.user.user_display_name)

  condition:
    #fail >= 10
}

rule aws_saml_identity_provider_changes {

  meta:
    author = "Chronicle Security"
    description = "Detects create, update or delete events of a SAML provider in AWS."
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v2.6"
    severity = "Medium"
    priority = "Medium"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    (
      $cloudtrail.metadata.product_event_type = "CreateSAMLProvider" or
      $cloudtrail.metadata.product_event_type = "UpdateSAMLProvider" or
      $cloudtrail.metadata.product_event_type = "DeleteSAMLProvider"
    )
    $cloudtrail.security_result.action = "ALLOW"

  outcome:
    $risk_score = max(
        // Unauthorized geographies
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Cuba", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Iran", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Russia", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Syria", 40)
    )
    $networkHttpUserAgent = $cloudtrail.network.http.user_agent
    $principalIp = array_distinct($cloudtrail.principal.ip)
    $principalIpCountry = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principalUserDisplayName = $cloudtrail.principal.user.user_display_name
    $recipientAwsAccount = $cloudtrail.target.resource.attribute.labels["Recipient Account Id"]
    $awsRegion = $cloudtrail.principal.location.name
    $targetResourceName = $cloudtrail.target.resource.name

  condition:
    $cloudtrail
}

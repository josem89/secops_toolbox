rule aws_new_mfa_method_registered_for_user {

  meta:
    author = "Chronicle Security"
    description = "Detects the registration of a new Multi Factor authentication method for an AWS user."
    mitre_attack_tactic = "Credential Access, Defense Evasion, Persistence"
    mitre_attack_technique = "Modify Authentication Process: Multi-Factor Authentication"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1556/006/"
    mitre_attack_version = "v1.0"
    severity = "Medium"
    priority = "Medium"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    (
    $cloudtrail.metadata.product_event_type = "CreateVirtualMFADevice" or 
    $cloudtrail.metadata.product_event_type = "EnableMFADevice"
    )
    $cloudtrail.security_result.action = "ALLOW"
    $cloudtrail.principal.user.user_display_name = $userId

  match:
    $userId over 1h

  outcome:
    $risk_score = max(
        // Unauthorized geographies
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Cuba", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Iran", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Russia", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Syria", 40) +

        // High Risk for AWS Root User
        if($cloudtrail.src.user.user_display_name = "AWS Root User", 50)
    )
    $networkHttpUserAgent = array_distinct($cloudtrail.network.http.user_agent)
    $principalIp = array_distinct($cloudtrail.principal.ip)
    $principalIpCountry = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principalUserDisplayName = array_distinct($cloudtrail.principal.user.user_display_name)
    $awsAccount = array_distinct($cloudtrail.target.resource.attribute.labels["Recipient Account Id"])
    $awsRegion = array_distinct($cloudtrail.principal.location.name)

  condition:
    $cloudtrail
}

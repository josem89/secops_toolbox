rule okta_multiple_users_logins_with_invalid_credentials_from_the_same_ip {

  meta:
    author = "Chronicle Security"
    description = "Detects multiple user logins with invalid credentials from a single IP."
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v2.5"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    $login.metadata.event_type = "USER_LOGIN"
    $login.metadata.product_event_type = "user.session.start"
    $login.security_result.summary = "User login to Okta"
    $login.security_result.action = "BLOCK"
    $login.security_result.category_details = "INVALID_CREDENTIALS"
    $login.principal.ip = $principal_ip

  match:
    $principal_ip over 1h

  outcome:
    $risk_score = max(
        // If the IP Address is marked as suspicious IP address by Okta ThreatInsight
        if($login.security_result.detection_fields["threatSuspected"] = "true", 30) +
        // Based on Okta Behavior Detection pattern analysis
        if($login.security_result.description = /New Geo-Location=POSITIVE/, 10) +
        if($login.security_result.description = /New Device=POSITIVE/, 5) +
        if($login.security_result.description = /New IP=POSITIVE/, 5) +
        if($login.security_result.description = /New City=POSITIVE/, 5) +
        if($login.security_result.description = /New State=POSITIVE/, 10) +
        if($login.security_result.description = /New Country=POSITIVE/, 10) +
        if($login.security_result.description = /Velocity=POSITIVE/, 10) +
        // Unauthorized target geographies
        if($login.target.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $targetBrowser = array_distinct($login.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($login.principal.ip)
    $principalIpCountry = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($login.principal.location.city)
    $dcPrincipalIpCity = array_distinct($login.principal.location.city)
    $securityResultSummary = array_distinct($login.security_result.summary)
    $principalUserManagersEmailAddresses = array_distinct($login.principal.user.managers.email_addresses)
    $principalUserUserid = array_distinct($login.principal.user.userid)
    $dcPrincipalUserUserid = count_distinct($login.principal.user.userid)
    $targetUserEmailAddresses = array_distinct($login.target.user.email_addresses)
    $targetUserUserid = array_distinct($login.target.user.userid)
    $securityResultDescription = array_distinct($login.security_result.description)

  condition:
    $login and $dcPrincipalUserUserid > 5
}

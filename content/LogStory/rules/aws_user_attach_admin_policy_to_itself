rule aws_user_attach_admin_policy_to_itself {

  meta:
    author = "Chronicle Security"
    description = "Detects when an AWS IAM user attaches AdministratorAccess policy to itself used for escalation of privileges."
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts: Cloud Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/004/"
    mitre_attack_version = "v1.5"
    severity = "High"
    priority = "High"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    $cloudtrail.metadata.product_event_type = "AttachUserPolicy"
    $cloudtrail.security_result.action = "ALLOW"
    $cloudtrail.target.resource.attribute.labels["Policy ARN"] = "arn:aws:iam::aws:policy/AdministratorAccess"
    $cloudtrail.src.user.user_display_name = $cloudtrail.principal.user.user_display_name

  outcome:
    $risk_score = max(
        // Unauthorized geographies
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Cuba", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Iran", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Russia", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Syria", 40)
    )
    $networkHttpUserAgent = $cloudtrail.network.http.user_agent
    $principalIp = array_distinct($cloudtrail.principal.ip)
    $principalIpCountry = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principalUserDisplayName = $cloudtrail.principal.user.user_display_name
    $awsAccount = $cloudtrail.target.resource.attribute.labels["Recipient Account Id"]
    $awsRegion = $cloudtrail.principal.location.name

  condition:
    $cloudtrail
}

rule logins_from_terminated_employees {
  meta:
    // This rule requires a context data source
    // Such as AZURE AD Context or workday
    // And showcases the natural correlation of context logs(Employee Context)
    // With event logs (authentication etc.)
    author = "Chronicle Security"
    description = "Allowed Logins from Terminated Employees"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.event_type = "USER_LOGIN"
    $login.security_result.action = "ALLOW"
    $login.target.user.userid = $user

    $login.target.user.termination_date.seconds < $login.metadata.event_timestamp.seconds 

    $login.target.user.termination_date.seconds > 0 

  match: 
      $user over 1d

  outcome:
    $employee_last_login = max($login.target.user.termination_date.seconds)
    $total_distinct_ips = count_distinct($login.principal.ip)
    $principalHostname = array_distinct($login.principal.hostname )
    $principalIp = array_distinct($login.principal.ip )
    $principalMac = array_distinct($login.principal.mac )
    $targetHostname = array_distinct($login.target.hostname )
    $targetIp = array_distinct($login.target.ip )
    $targetMac = array_distinct($login.target.mac )
    $target_user_userid =  array_distinct($login.target.user.userid)
    $target_email = array_distinct($login.target.email)

  condition:
    $login
}

rule vt_relationships_file_contacts_domain {
  meta:
    author = "Chronicle Security"
    description = "Alert on known Hash querying a known hostname with VT Relationships."
    severity = "High"
    priority = "High"

  events:
    // event
    $e.metadata.event_type = "NETWORK_DNS"
    $e.principal.process.file.sha256 = $file_hash
    $e.network.dns.questions.name = $domain
    strings.coalesce($e.principal.hostname, $e.target.user.userid) = $hostname_or_userid


    // principal
    $vt.graph.metadata.entity_type = "FILE"
    $vt.graph.metadata.source_type = "GLOBAL_CONTEXT"
    $vt.graph.metadata.vendor_name = "VirusTotal"
    $vt.graph.metadata.product_name = "VirusTotal Relationships"
    $vt.graph.entity.file.sha256 = $file_hash

    // target
    $vt.graph.relations.entity_type = "DOMAIN_NAME"
    $vt.graph.relations.relationship = "CONTACTS"
    $vt.graph.relations.entity.domain.name = $domain

  match:
    $hostname_or_userid over 5m

  outcome:
    $p_hostname = array_distinct($e.principal.hostname)
    $p_sha256 = array_distinct($e.principal.process.file.sha256)
    $p_md5 = array_distinct($e.principal.process.file.md5)
    $p_sha1 = array_distinct($e.principal.process.file.sha1)
    $p_ip = array_distinct($e.principal.ip)
    $p_mac = array_distinct($e.principal.mac)
    $p_userid = array_distinct($e.target.user.userid)
    $p_domain = array_distinct($e.network.dns.questions.name)

  condition:
    $e and $vt
}

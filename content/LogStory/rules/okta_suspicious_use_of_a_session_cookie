rule okta_suspicious_use_of_a_session_cookie {

  meta:
    author = "Chronicle Security"
    description = "Detects when an adversary attempts to reuse a stolen web session cookie in a different device that has a different OS, IP, Browser or User Agent."
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Steal Web Session Cookie"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1539/"
    mitre_attack_version = "v1.2"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    $login.metadata.event_type = "USER_UNCATEGORIZED"
    $login.metadata.product_event_type = "policy.evaluate_sign_on"
    (
        $login.security_result.action = "ALLOW" or
        $login.security_result.action = "ALLOW_WITH_MODIFICATION" or
        $login.security_result.action = "CHALLENGE"
    )
    $login.principal.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $targetOs = array_distinct($login.network.http.parsed_user_agent.os)
    $dcTargetOs = count_distinct($login.network.http.parsed_user_agent.os)
    $targetBrowser = array_distinct($login.network.http.parsed_user_agent.browser)
    $dcTargetBrowser = count_distinct($login.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($login.principal.ip)
    $dcPrincipalIp = count_distinct($login.principal.ip)
    $principalIpCountry = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($login.principal.location.city)
    $dcPrincipalIpCity = count_distinct($login.principal.location.city)
    $securityResultSummary = array_distinct($login.security_result.summary)
    $principalUserManagersEmailAddresses = array_distinct($login.principal.user.managers.email_addresses)
    $principalUserUserid = array_distinct($login.principal.user.userid)

  condition:
    $login and $dcPrincipalIp > 1 and ($dcTargetBrowser > 1 or $dcTargetOs > 1)
}

rule google_safebrowsing_file_process_creation {
    meta:
        author = "Chronicle Security"
        description = "Detection of process/file events that match Safe Browsing hashes, which are files deemed to be malicious"
        severity = "High"
        priority = "High"

    events:
        // find a process launch event, match on hostname
        ($execution.metadata.event_type = "PROCESS_LAUNCH" or $execution.metadata.event_type = "FILE_CREATION")
        $execution.principal.hostname = $hostname
        $execution.target.process.file.sha256 = $sha256	
   
        // join execution event with Safe Browsing graph
        $safebrowse.graph.entity.file.sha256 = $sha256
   
        // look for files deemed malicious
        $safebrowse.graph.metadata.product_name = "Google Safe Browsing"
        $safebrowse.graph.metadata.entity_type = "FILE"
        $safebrowse.graph.metadata.source_type = "GLOBAL_CONTEXT"
        
        // Can comment the following two lines to adjust the safe browsing hashes available for correlation
        $safebrowse.graph.metadata.threat.category = "SOFTWARE_MALICIOUS"
        $safebrowse.graph.metadata.threat.severity = "CRITICAL"

    match:
        $hostname over 1h

    condition:
        $execution and $safebrowse
}

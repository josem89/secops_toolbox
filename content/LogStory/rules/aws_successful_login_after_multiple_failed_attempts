rule aws_successful_login_after_multiple_failed_attempts {

  meta:
    author = "Chronicle Security"
    description = "Detects successful login after multiple failed attempts"
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v2.5"
    severity = "Medium"
    priority = "Medium"

  events:
    $fail.metadata.vendor_name = "AMAZON"
    $fail.metadata.product_name = "AWS CloudTrail"
    $fail.metadata.event_type = "USER_LOGIN"
    $fail.metadata.product_event_type = "ConsoleLogin"
    $fail.security_result.action = "BLOCK"
    $fail.security_result.description = "Reason: Failed authentication"
    $fail.principal.user.product_object_id = $userId 

    $success.metadata.vendor_name = "AMAZON"
    $success.metadata.product_name = "AWS CloudTrail"
    $success.metadata.event_type = "USER_LOGIN"
    $success.metadata.product_event_type = "ConsoleLogin"
    $success.security_result.action = "ALLOW"
    $success.security_result.summary = "Login to AWS Console succeeded."
    $success.target.user.product_object_id = $userId 

    $success.metadata.event_timestamp.seconds > $fail.metadata.event_timestamp.seconds

  match:
    $userId over 15m

  outcome:
    $risk_score = max(
        // Country mismatch
        if($fail.principal.ip_geo_artifact.location.country_or_region = $success.principal.ip_geo_artifact.location.country_or_region, 40) +
        // Unauthorized geographies
        if($success.principal.ip_geo_artifact.location.country_or_region = "Cuba", 10) +
        if($success.principal.ip_geo_artifact.location.country_or_region = "Iran", 10) +
        if($success.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 10) +
        if($success.principal.ip_geo_artifact.location.country_or_region = "Russia", 10) +
        if($success.principal.ip_geo_artifact.location.country_or_region = "Syria", 10)
    )
    $principalIp = array_distinct($success.principal.ip)
    $principalIpCountry = array_distinct($success.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($success.principal.ip_geo_artifact.location.state)
    $targetUserUserid = array_distinct($success.target.user.userid)
    $mfaUsed = array_distinct($success.extensions.auth.auth_details)

  condition:
    #fail > 3 and $success
}

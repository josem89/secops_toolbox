rule okta_threatinsight_login_failure_with_high_unknown_users {

  meta:
    author = "Chronicle Security"
    description = "Okta's ThreatInsight can identify multiple login failures with high unknown users count from the same IP across one or more Okta orgs."
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force: Credential Stuffing"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/004/"
    mitre_attack_version = "v1.2"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    $login.metadata.event_type = "USER_UNCATEGORIZED"
    $login.metadata.product_event_type = "security.threat.detected"
    $login.security_result.category_details = /Login failures with high unknown users count/
    $login.principal.user.userid = $userid
    $login.principal.ip = $principal_ip

  match:
    $userid, $principal_ip over 1h

  outcome:
    $risk_score = max(
        // If the IP Address is marked as suspicious IP address by Okta ThreatInsight
        if($login.security_result.detection_fields["threatSuspected"] = "true", 30) +
        // Unauthorized target geographies
        if($login.principal.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $targetBrowser = array_distinct($login.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($login.principal.ip)
    $principalIpCountry = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($login.principal.location.city)
    $securityResultSummary = array_distinct($login.security_result.summary)

  condition:
    $login
}

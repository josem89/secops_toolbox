rule okta_threatinsight_suspected_brute_force_attack {

  meta:
    author = "Chronicle Security"
    description = "Okta ThreatInsight detects multiple login failures from the same IP across one or more Okta orgs."
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force: Password Guessing"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/001/"
    mitre_attack_version = "v1.3"
    severity = "Medium"
    priority = "Medium"

  events:
    $bruteforce.metadata.product_name = "Okta"
    $bruteforce.metadata.vendor_name = "Okta"
    $bruteforce.metadata.event_type = "USER_UNCATEGORIZED"
    $bruteforce.metadata.product_event_type = "security.threat.detected"
    $bruteforce.security_result.category_details = "Login Failures"
    $bruteforce.principal.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $risk_score = max(
        // If the IP Address is marked as suspicious IP address by Okta ThreatInsight
        if($bruteforce.security_result.detection_fields["threatSuspected"] = "true", 30) +
        // Unauthorized target geographies
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $targetBrowser = array_distinct($bruteforce.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($bruteforce.principal.ip)
    $principalIpCountry = array_distinct($bruteforce.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($bruteforce.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($bruteforce.principal.location.city)
    $securityResultSummary = array_distinct($bruteforce.security_result.summary)

  condition:
    $bruteforce
}

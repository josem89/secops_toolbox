rule gcti_tor_exit_nodes {

 meta:
  author = "Chronicle Security"
  description = "Alert on known Tor exit nodes."
  severity = "High"
  priority = "High"

 events:
  $event.metadata.event_type = "NETWORK_CONNECTION"
  $event.target.ip = $tor_ip
  $gcti_feed.graph.entity.artifact.ip = $tor_ip
  $gcti_feed.graph.metadata.entity_type = "IP_ADDRESS"
  $gcti_feed.graph.metadata.threat.threat_feed_name = "Tor Exit Nodes"
  $gcti_feed.graph.metadata.product_name = "GCTI Feed"
  $gcti_feed.graph.metadata.source_type = "GLOBAL_CONTEXT"

 match:
  $tor_ip over 1h

 outcome:
  $tor_ips = array_distinct($tor_ip)
  $tor_geoip_country = array_distinct($event.target.ip_geo_artifact.location.country_or_region)
  $tor_geoip_state = array_distinct($event.target.ip_geo_artifact.location.state)

 condition:
  $event and $gcti_feed
}

rule dns_query_to_recently_created_domain {

  meta:
    author = "Chronicle Security"
    description = "DNS query to a recently created domain"
    severity = "Low"
    priority = "Low"

  events:
    $event.metadata.event_type = "NETWORK_DNS"
    $event.metadata.vendor_name != "Extrahop" //Removing ExtraHop as generates too much noise
    $event.network.dns.questions.name = $dnsQuery
    $event.principal.user.userid = $user

    $entity.graph.entity.hostname = $dnsQuery
    $entity.graph.metadata.entity_type = "DOMAIN_NAME"
    $entity.graph.metadata.vendor_name = "WHOIS"
    $entity.graph.entity.domain.creation_time.seconds > 0
    // domain was created in the last 30 days
    2592000 > timestamp.current_seconds() - $entity.graph.entity.domain.creation_time.seconds

  match:
    $user over 1h

  outcome:
    $principalHostname = array_distinct($event.principal.hostname)
    $principalUserUserid = array_distinct($event.principal.user.userid)
    $principalIp = array_distinct($event.principal.ip)
    $principalMac = array_distinct($event.principal.mac)
    $networkDnsQuestionsName = array_distinct($event.network.dns.questions.name)
    $networkDnsAnswersData = array_distinct($event.network.dns.answers.data)
    $entityDomainName = array_distinct($entity.graph.entity.domain.name)

  condition:
    $event and $entity
}

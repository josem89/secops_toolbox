rule aws_password_policy_change {

  meta:
    author = "Chronicle Security"
    description = "Detects when an existing password policy is updated or deleted in an AWS account."
    mitre_attack_tactic = "Discovery"
    mitre_attack_technique = "Password Policy Discovery"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1201/"
    mitre_attack_version = "v1.5"
    severity = "Medium"
    priority = "Medium"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    (
     $cloudtrail.metadata.product_event_type = "UpdateAccountPasswordPolicy" or 
     $cloudtrail.metadata.product_event_type = "DeleteAccountPasswordPolicy"
    )
    $cloudtrail.security_result.action = "ALLOW"

  outcome:
    $risk_score = max(
        // Unauthorized geographies
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Cuba", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Iran", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "North Korea" , 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Russia", 40) +
        if($cloudtrail.principal.ip_geo_artifact.location.country_or_region = "Syria", 40)
    )
    $networkHttpUserAgent = $cloudtrail.network.http.user_agent
    $principalIp = array_distinct($cloudtrail.principal.ip)
    $principalIpCountry = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principalUserDisplayName = $cloudtrail.principal.user.user_display_name
    $recipientAwsAccount = $cloudtrail.target.resource.attribute.labels["Recipient Account Id"]
    $awsRegion = $cloudtrail.principal.location.name
    $IsCrossAccountAccess = array_distinct(if($cloudtrail.target.resource.attribute.labels["Recipient Account Id"] != $cloudtrail.principal.user.group_identifiers, "true","false"))

  condition:
    $cloudtrail
}

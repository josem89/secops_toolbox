rule whois_expired_domain_executable_downloaded {
 
 meta:
   author = "Chronicle Security"
   description = "Detect web traffic to a recently expired domain followed by an exe file creation event"
   severity = "Low"
   priority = "Low"

 events:
        $access.metadata.event_type = "NETWORK_HTTP"
        $access.principal.hostname = $hostname
        $access.target.hostname = $domain
        
        // join access event to entity graph to use WHOIS data
        $whois.graph.entity.domain.name = $domain

        // use WHOIS data to look for expired domains
        $whois.graph.metadata.entity_type = "DOMAIN_NAME"
        $whois.graph.metadata.vendor_name = "WHOIS"
        $whois.graph.metadata.product_name = "WHOISXMLAPI Simple Whois"
        $whois.graph.metadata.source_type = "GLOBAL_CONTEXT"

        // Domain expired before the network event
        $whois.graph.entity.domain.expiration_time.seconds < $access.metadata.event_timestamp.seconds 

        // join access event with executable file creation event by principal hostname
        $creation.principal.hostname = $hostname
        $creation.metadata.event_type = "FILE_CREATION"
        $creation.target.file.full_path = /exe/ nocase

        // file creation comes after network event
        $creation.metadata.event_timestamp.seconds > $access.metadata.event_timestamp.seconds

   match:
       $hostname over 1h

 condition:
        $access and $whois and $creation
}

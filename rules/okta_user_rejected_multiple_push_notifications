rule okta_user_rejected_multiple_push_notifications {

  meta:
    author = "Chronicle Security"
    description = "Detects when an Okta user rejects more than 2 Push notifications in a 10 minute window."
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/"
    mitre_attack_version = "v2.4"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    $login.metadata.event_type = "USER_UNCATEGORIZED"
    $login.metadata.product_event_type = "user.mfa.okta_verify.deny_push"
    $login.security_result.summary = "User rejected Okta push verify"
    $login.security_result.action = "BLOCK"
    $login.principal.user.userid = $userid

  match:
    $userid over 10m

  outcome:
    $targetBrowser = array_distinct($login.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($login.principal.ip)
    $principalIpCountry = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($login.principal.location.city)
    $principalUserEmailAddresses = array_distinct ($login.principal.user.email_addresses)
    $securityResultSummary = array_distinct($login.security_result.summary)

  condition:
    #login >= 2
}

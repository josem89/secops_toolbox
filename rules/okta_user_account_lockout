rule okta_user_account_lockout {

  meta:
    author = "Chronicle Security"
    description = "Detects when a user's account is locked out or a user account has reached the lockout limit."
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v2.6"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    (
        $login.metadata.product_event_type = "user.account.lock.limit" or
        $login.metadata.product_event_type = "user.account.lock"
    )
    $login.security_result.action = "BLOCK"
    $login.principal.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $targetBrowser = array_distinct($login.network.http.parsed_user_agent.browser)
    $principalIp = array_distinct($login.principal.ip)
    $principalIpCountry = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principalIpState = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principalIpCity = array_distinct($login.principal.location.city)
    $principalUserEmailAddresses = array_distinct ($login.principal.user.email_addresses)
    $securityResultSummary = array_distinct($login.security_result.summary)
    $securityResultCategoryDetails = array_distinct($login.security_result.category_details)

  condition:
    $login
}

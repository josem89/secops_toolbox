rule vt_relationships_file_downloaded_from_ip {
  meta:
    author = "Chronicle Security"
    description = "Alert on downloading a known file hash from a known IP with VT Relationships."
    severity = "High"
    priority = "High"

  events:
    // NETWORK_HTTP
    $e1.metadata.event_type = "NETWORK_HTTP"
    $e1.principal.user.userid = $userid
    $e1.target.ip = $ip

    // FILE_CREATION
    $e2.metadata.event_type = "FILE_CREATION"
    $e2.target.user.userid = $userid
    $e2.target.file.sha256 = $file_hash

    // First NETWORK_HTTP later FILE_CREATION
    $e1.metadata.event_timestamp.seconds <= $e2.metadata.event_timestamp.seconds

    // principal
    $vt.graph.metadata.entity_type = "FILE"
    $vt.graph.metadata.source_type = "GLOBAL_CONTEXT"
    $vt.graph.metadata.vendor_name = "VirusTotal"
    $vt.graph.metadata.product_name = "VirusTotal Relationships"
    $vt.graph.entity.file.sha256 = $file_hash

    // target
    $vt.graph.relations.entity_type = "IP_ADDRESS"
    $vt.graph.relations.relationship = "DOWNLOADED_FROM"
    $vt.graph.relations.entity.ip = $ip

  match:
    $userid over 5m

  condition:
    $e1 and $e2 and $vt
}

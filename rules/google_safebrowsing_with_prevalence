 rule google_safebrowsing_with_prevalence {
  meta:
    author = "Chronicle Security"
    severity = "Low"
    priority = "Low"

  events:
    // find a process launch event    
    $execution.metadata.event_type = "PROCESS_LAUNCH"
    $execution.target.process.file.sha256 != ""
    $execution.principal.hostname = $hostname
    $execution.target.process.file.sha256 = $hash

    // look for files deemed malicious by SafeBrowsing
    $safebrowsing.graph.metadata.entity_type = "FILE"
    $safebrowsing.graph.metadata.threat.severity = "CRITICAL"
    $safebrowsing.graph.metadata.product_name = "Google Safe Browsing"
    $safebrowsing.graph.metadata.source_type = "GLOBAL_CONTEXT"
    $safebrowsing.graph.entity.file.sha256 = $hash
    
    //prevalence entity graph lookup
    $prevalence.graph.metadata.entity_type = "FILE"
    $prevalence.graph.metadata.source_type = "DERIVED_CONTEXT"
    $prevalence.graph.entity.file.prevalence.day_count = 10
    $prevalence.graph.entity.file.prevalence.rolling_max <= 3
    $prevalence.graph.entity.file.sha256 = $hash

  match:
      $hostname, $hash over 5m

  condition:
      $execution and $safebrowsing and $prevalence
}
